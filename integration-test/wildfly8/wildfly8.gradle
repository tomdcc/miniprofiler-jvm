/*
 * Copyright 2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id("build.browser-test")
    id("build.cargo-test")
    id("build.java-module")
}

dependencies {
    implementation project(':miniprofiler-javaee')
    implementation project(':miniprofiler-servlet')
}


def wildflyBaseDir = file("$buildDir/wildfly")
task wildflyDir {
    doLast {
	    wildflyBaseDir.mkdirs()
    }
}
[cargoRunLocal, cargoStartLocal]*.dependsOn(wildflyDir)


cargo {
	containerId = 'wildfly8x'
	port = 8081
	deployable {
		context = '/'
	}

	local {
		installer {
			installUrl = 'https://download.jboss.org/wildfly/8.1.0.Final/wildfly-8.1.0.Final.tar.gz'
			downloadDir = rootProject.file(".gradle/cache/cargo")
			extractDir = file("$buildDir/extract")
		}

		configHomeDir = wildflyBaseDir
		homeDir = wildflyBaseDir

        containerProperties {
            property 'cargo.java.home', javaToolchains.launcherFor { languageVersion = JavaLanguageVersion.of(8) }.get().metadata.installationPath.asFile.absolutePath
        }

        file {
			file = configurations.cargo.files.find { it.name.startsWith('h2-') }
			toDir = new File("lib")
		}
	}
}
