buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.5'
	}
}

apply plugin: 'war'
apply plugin: 'cargo'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compileOnly 'javax:javaee-api:7.0'

	def cargoVersion = '1.4.8'
	cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion"
	cargo "org.codehaus.cargo:cargo-ant:$cargoVersion"
	cargo 'com.h2database:h2:1.3.173'

}


test {
	systemProperties "geb.build.reportsDir": "$reporting.baseDir/geb"
}

def wildflyBaseDir = file("$buildDir/wildfly")
task wildflyDir {
    doLast {
	    wildflyBaseDir.mkdirs()
    }
}

[cargoRunLocal, cargoStartLocal]*.dependsOn([':miniprofiler-core:assemble', assemble, wildflyDir])

test.dependsOn(cargoStartLocal)
cargoStartLocal.finalizedBy(cargoStopLocal)
cargoStopLocal.mustRunAfter(test)

cargo {
	containerId = 'wildfly8x'
	port = 8081
	deployable {
		context = '/'
	}

	local {
		installer {
			installUrl = 'http://download.jboss.org/wildfly/8.1.0.Final/wildfly-8.1.0.Final.tar.gz'
			downloadDir = rootProject.file(".gradle/cache/cargo")
			extractDir = buildDir
		}

		configHomeDir = wildflyBaseDir
		homeDir = wildflyBaseDir

		file {
			file = configurations.cargo.files.find { it.name.startsWith('h2-') }
			toDir = new File("lib")
		}
	}
}

idea {
	module {
		jdkName = '1.7'
	}
}
